plugins {
  id 'org.springframework.boot' version '2.5.6'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

sourceCompatibility = JavaVersion.VERSION_17

dependencies {
  implementation project(':methanol')
  implementation project(':methanol-jackson')
  implementation project(':methanol-testing')
  implementation libs.mockwebserver
  // Must explicitly declare okhttp dep to avoid a weird NoClassDefFoundError due to an old
  // okhttp version spring-boot puts in the boot jar.
  implementation libs.okhttp
  implementation libs.springboot.starter.web
  implementation libs.autoservice.annotations
  annotationProcessor libs.autoservice.annprocess
}

bootJar {
  mainClass = 'com.github.mizosoft.methanol.springboot.SpringBootApp'
}

test {
  dependsOn bootJar
  doFirst {
    systemProperty('com.github.mizosoft.methanol.bootJar.path', bootJar.archiveFile.get())
  }
}
