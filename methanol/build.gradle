dependencies {
  testImplementation deps.reactiveStreamsTckFlow
  testImplementation deps.mockWebServer
  testImplementation project(':methanol-testutils')
}

compileJava {
  // suppress exporting to module unresolvable on compilation
  options.compilerArgs += '-Xlint:-module'
}

test {
  exclude '**/tck/**'
  // run from classpath to use META-INF/services which doesn't work from module path
  moduleOptions {
    runOnClasspath = true
  }
}

task tckTest(type: Test) {
  useTestNG()
  include '**/tck/**'
  shouldRunAfter test
  scanForTestClasses false
  testLogging {
    events = ['failed']
  }

  if (System.getenv().GITHUB_ACTIONS) {
    systemProperties.TCK_TIMEOUT_MILLIS = 1_000
    systemProperties.TCK_NO_SIGNAL_TIMEOUT_MILLIS = 100
  }
}

check.dependsOn tckTest

// compileTestJava still compiles from the module path
addReads(compileTestJava, 'methanol.testutils')
